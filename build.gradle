plugins {
	id 'dev.architectury.loom' version '1.2-SNAPSHOT'
	id 'io.github.juuxel.loom-vineflower' version '1.+'
	id 'java-library'
	id 'maven-publish'
	id 'com.modrinth.minotaur' version '2.+'
	id 'net.darkhax.curseforgegradle' version '1.+'
}

group = project.maven_group
version = "${project.mod_version}+${project.minecraft_version}"
archivesBaseName = project.archives_base_name

// This field defines the Java version your mod target.
def targetJavaVersion = 17

loom {
	accessWidenerPath = file("src/main/resources/lambdynlights.accesswidener")

	forge {
		convertAccessWideners = true
		extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

		mixinConfigs = [
				"lambdynlights.mixins.json","lambdynlights.lightsource.mixins.json"
		]
	}
}

repositories {
	maven {
		name "Quilt"
		url "https://maven.quiltmc.org/repository/release"
	}
	exclusiveContent {
		forRepository {
			maven {
				name "Modrinth"
				url "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${project.quilt_mappings}:intermediary-v2"
	forge "net.minecraftforge:forge:${project.forge_version}"

	modImplementation("dev.architectury:architectury-forge:${project.architectury_version}")

	modImplementation("maven.modrinth:obsidianui:${project.spruceui_version}")

	modCompileOnly "maven.modrinth:rubidium:${project.rubidium_version}"
	modCompileOnly "maven.modrinth:embeddium:${project.embeddium_version}"
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'

	it.options.release.set(targetJavaVersion)
}

processResources {
	inputs.property 'version', project.version

	filesMatching('META-INF/mods.toml') {
		expand 'version': project.version
	}
}

jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")

	projectId = "${project.modrinth_id}"
	versionNumber = "${project.version}"
	versionType = "beta"
	changelog = file("CHANGELOG.md").getText('UTF-8')
	uploadFile = remapJar
	additionalFiles = [sourcesJar]
	gameVersions = ["1.20", "1.20.1"]
	loaders = ["forge", "neoforge"]
	dependencies {
		required.project "obsidianui"
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("CURSEFORGE_TOKEN")
	disableVersionDetection()

	def mainFile = upload(project.curseforge_id, remapJar)
	mainFile.displayName = "${project.version}"
	mainFile.releaseType = "beta"
	mainFile.changelog = file("CHANGELOG.md")
	mainFile.changelogType = "markdown"
	mainFile.addModLoader("Forge", "NeoForge")
	mainFile.addJavaVersion("Java 17", "Java 18")
	mainFile.addGameVersion("1.20", "1.20.1")
	mainFile.addRequirement("obsidianui")

	def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
	sourcesFile.changelog = file("CHANGELOG.md")
	sourcesFile.changelogType = "markdown"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
